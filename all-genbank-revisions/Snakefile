rule all:
    input:
        metadata="results/multi_revision_metadata.tsv.zst",
        sequences="results/multi_revision_sequences.fasta.zst",
        nextclade="results/nextclade.tar.zst",


rule download_old_metadata:
    """
    From 2023-01-19T01:25:09+00:00
    """
    output:
        "data/metadata_old.tsv.zst",
    shell:
        """
        aws s3api get-object \
        --bucket nextstrain-data \
        --key files/ncov/open/metadata.tsv.zst \
        --version-id AnF_xO4iudTVdiocg0yCFmP6nyvwiVLt \
        2> /dev/null \
        {output}
        """


rule download_old_sequences:
    output:
        "data/sequences_old.fasta.zst",
    shell:
        """
        aws s3api get-object \
        --bucket nextstrain-data \
        --key files/ncov/open/sequences.fasta.zst \
        --version-id "7p8eLHr52750qwI061ahzg3ykPzyZ.J2" \
        {output}
        """


rule download_new_metadata:
    output:
        "data/metadata_new.tsv.zst",
    shell:
        """
        aws s3 cp s3://nextstrain-data/files/ncov/open/metadata.tsv.zst {output}
        """


rule download_new_sequences:
    output:
        "data/sequences_new.fasta.zst",
    shell:
        """
        aws s3 cp s3://nextstrain-data/files/ncov/open/sequences.fasta.zst {output}
        """


rule download_nextclade_dataset:
    output:
        "data/nextclade.zip",
    shell:
        """
        nextclade dataset get --name sars-cov-2 --output-zip {output}
        """


"""
Could do it much more simply by just appending and uniq'ing
Oh well...
"""


rule select_accession_cols:
    input:
        "data/metadata_{age}.tsv.zst",
    output:
        "results/accession_cols_{age}.tsv",
    shell:
        """
        zstdcat {input} \
        | tsv-select -H -f strain,genbank_accession,genbank_accession_rev \
        > {output}
        """


rule non_first_accessions:
    input:
        "results/accession_cols_{age}.tsv",
    output:
        "results/non_first_accessions_{age}.tsv",
    shell:
        """
        tsv-filter -H --str-in-fld genbank_accession_rev:"." --str-not-in-fld genbank_accession_rev:".1" {input} \
        > {output}
        """


rule join_old_to_new_non_first:
    """
    Join old accession numbers to new accession numbers for non-first accessions
    """
    input:
        old_accessions="results/accession_cols_old.tsv",
        new_non_first="results/non_first_accessions_new.tsv",
    output:
        "results/non_first_accessions.tsv",
    shell:
        """
        tsv-join -H \
        --filter-file {input.new_non_first} \
        --key-fields genbank_accession \
        --append-fields genbank_accession_rev \
        --prefix new_ \
        {input.old_accessions} \
        > {output}
        """


rule filter_changed:
    """
    Filter out accessions/rows that have not changed
    """
    input:
        "results/non_first_accessions.tsv",
    output:
        "results/changed_accessions.tsv",
    shell:
        """
        tsv-filter -H \
        --ff-str-ne new_genbank_accession_rev:genbank_accession_rev \
        {input} \
        > {output}
        """


rule get_changed_old_metadata:
    """
    Get metadata for changed accessions from old metadata
    """
    input:
        old_meta="data/metadata_old.tsv.zst",
        changed_accessions="results/changed_accessions.tsv",
    output:
        "results/changed_old_metadata.tsv.zst",
    shell:
        """
        zstdcat {input.old_meta} \
        | tsv-join -H \
        --filter-file {input.changed_accessions} \
        --key-fields genbank_accession \
        | zstd > {output}
        """


rule append_old_metadata:
    """
    Append metadata of old revisions to new metadata
    """
    input:
        old_meta="results/changed_old_metadata.tsv.zst",
        new_meta="data/metadata_new.tsv.zst",
    output:
        "results/multi_revision_metadata.tsv.zst",
    shell:
        """
        zstdcat {input.old_meta} {input.new_meta} \
        | zstd > {output}
        """


rule replace_seq_id_with_rev:
    """
    Replace ids using a mapping file
    """
    input:
        seq="data/sequences_{age}.fasta.zst",
        mapping="results/accession_cols_{age}.tsv",
    output:
        "results/rev_id_sequences_{age}.tsv.zst",
    shell:
        """
        set +o pipefail
        zstdcat {input.seq} \
        | seqkit fx2tab \
        | tsv-join \
        --filter-file {input.mapping} \
        --key-fields 1 \
        --append-fields 3 \
        | tsv-select -f 4,2 \
        | seqkit tab2fx -w0 \
        | zstd > {output}
        """


rule old_accessions_to_select:
    input:
        "results/changed_accessions.tsv",
    output:
        "results/old_to_select.txt",
    shell:
        """
        tsv-select -H \
        -f genbank_accession_rev \
        {input} \
        > {output}
        """


rule select_old_revisions:
    """
    From old sequences select those that have changed in new
    """
    input:
        old_seqs="results/rev_id_sequences_old.tsv.zst",
        filter="results/old_to_select.txt",
    output:
        "results/old_revisions.fasta.zst",
    shell:
        """
        zstdcat {input.old_seqs} \
        | seqkit grep -f {input.filter} -w0 \
        | zstd > {output}
        """


rule join_fastas:
    """
    Join old and new sequences
    """
    input:
        old="results/old_revisions.fasta.zst",
        new="results/rev_id_sequences_new.tsv.zst",
    output:
        "results/multi_revision_sequences.fasta.zst",
    shell:
        """
        zstdcat {input.old} {input.new} \
        | zstd > {output}
        """


rule run_nextclade:
    input:
        dataset="data/nextclade.zip",
        sequences="results/multi_revision_sequences.fasta.zst",
    output:
        tsv="results/nextclade/nextclade.tsv",
    shell:
        """
        zstdcat {input.sequences} \
        | nextclade run \
        --input-dataset {input.dataset} \
        --output-all results/nextclade \
        --output-selection tsv,fasta,translations,insertions \
        """


rule compress_nextclade_results:
    input:
        "results/nextclade/nextclade.tsv",
    output:
        "results/nextclade.tar.zst",
    shell:
        """
        tar cvf - results/nextclade | zstd > {output}
        """
